plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '7.0.6'
}

version = forge_mod_version
group = mod_group_id
base.archivesName = mod_id

// Mojang ships Java 25 to end users in 26.1+, so your mod should target Java 25.
java.toolchain.languageVersion = JavaLanguageVersion.of(25)

// Include generated resources
sourceSets.main.resources { srcDir 'src/generated/resources' }

minecraft {
    mappings channel: 'official', version: "${project.minecraft_version}"

    accessTransformer = true

    runs {
        configureEach {
            workingDir = layout.projectDirectory.dir('run')

            systemProperty 'forge.logging.markers', 'REGISTRIES'

            systemProperty 'forge.logging.console.level', 'debug'

            systemProperty 'eventbus.api.strictRuntimeChecks', 'true'

            args "-mixin.config=${mod_id}.mixins.json"
        }



        register('client'){
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }

        register('server'){
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        register('gameTestServer'){
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }

        register('data'){
            workingDir = layout.projectDirectory.dir('run-data')
            args '--mod', mod_id, '--all', '--output', layout.projectDirectory.dir('src/generated/resources'), '--existing', layout.projectDirectory.dir('src/main/resources')
        }
    }
}

repositories {
    minecraft.mavenizer(it) // In Kotlin, it = this
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter {
            includeGroupAndSubgroups('org.spongepowered')
        }
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation minecraft.dependency("net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}")
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'
    //compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    //compileOnly "mezz.jei:jei-${mc_version}-forge-api:${jei_version}"
    //runtimeOnly "mezz.jei:jei-${mc_version}-forge:${jei_version}"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
        minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
        forge_version: forge_version, forge_version_range: forge_version_range,
        loader_version_range: loader_version_range,
        mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, forge_mod_version: forge_mod_version,
        mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.property('replaceProperties', replaceProperties)

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}

tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Specification-Title'     : mod_id,
            'Specification-Vendor'    : mod_authors,
            'Specification-Version'   : '1', // We are version 1 of ourselves
            'Implementation-Title'    : project.name,
            'Implementation-Version'  : project.jar.archiveVersion,
            'Implementation-Vendor'   : mod_authors
        ])
        attributes['MixinConfigs'] = "${mod_id}.mixins.json"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.maven_group
            artifactId project.archives_base_name
            version project.version
            artifact jar
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        if (System.getenv("MAVEN_URL")) {
            maven {
                name = 'mavenLocal2'
                url System.getenv("MAVEN_URL")
                // Add repositories to publish to here.
                // Notice: This block does NOT have the same function as the block in the top level.
                // The repositories here will be used for publishing your artifact, not for
                // retrieving dependencies.
            }
        }
    }
}

idea.module { downloadJavadoc = downloadSources = true }

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

eclipse {
    classpath { downloadJavadoc = downloadSources = true }
}